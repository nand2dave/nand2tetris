// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
		// counter
		Inc16(in=outRegister, out=outInc);
		Mux16(a=outRegister, b=outInc, sel=inc, out=outMuxInc);
		// load
		Mux16(a=outMuxInc, b=in, sel=load, out=outMuxLoad);
		// reset
		Mux16 (a=outMuxLoad, b=false, sel=reset, out=outMuxReset);
		// store value
		Register(in=outMuxReset, load=true, out=outRegister, out=out);
}
